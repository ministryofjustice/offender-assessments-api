import org.springframework.boot.gradle.plugin.SpringBootPlugin

import java.time.Instant
import java.time.LocalDate
import java.time.format.DateTimeFormatter

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.1.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id "com.gorylenko.gradle-git-properties" version "2.0.0"
    id 'com.github.ben-manes.versions' version '0.21.0'
}

project.sourceCompatibility = 11
project.targetCompatibility = 11

def applicationName = 'assessments-api'
def today = Instant.now()
def todayDate = LocalDate.now()
def formatter = DateTimeFormatter.ofPattern("yyyy-MM-dd")
version todayDate.format(formatter)

jar {
    baseName = applicationName
    manifest {
        attributes 'Implementation-Title': applicationName,
                'Implementation-Version': System.getenv('CI') ? "${version}.${System.getenv('CIRCLE_BUILD_NUM')}" : version
    }
}

springBoot {
    buildInfo {
        properties {
            artifact = rootProject.name
            version = System.getenv('CI') ? "${version}.${System.getenv('CIRCLE_BUILD_NUM')}" : version
            group = 'uk.gov.justice.digital'
            name = rootProject.name
            time = today
            additional = [
                    by                   : System.properties['user.name'],
                    operatingSystem      : "${System.properties['os.name']} (${System.properties['os.version']})",
                    continuousIntegration: System.getenv('CI') ? true : false,
                    machine              : InetAddress.localHost.hostName
            ]
        }
    }
}

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom(SpringBootPlugin.BOM_COORDINATES)
    }
}

// In this section you declare the dependencies for your production and test code
dependencies {
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.8'

    implementation files('lib/ojdbc8-12.2.0.1.jar')

    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.9.9'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.9'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.hateoas:spring-hateoas:0.25.1.RELEASE'
    implementation 'org.springframework.security.oauth:spring-security-oauth2:2.3.6.RELEASE'
    implementation 'org.springframework.security:spring-security-jwt:1.0.10.RELEASE'

    implementation 'com.github.timpeeters:spring-boot-graceful-shutdown:2.1.1'

    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'javax.xml.bind:jaxb-api:2.3.0'
    implementation 'javax.activation:activation:1.1.1'
    implementation 'com.sun.xml.bind:jaxb-impl:2.3.0'
    implementation 'com.sun.xml.bind:jaxb-core:2.3.0.1'

    implementation 'org.hibernate:hibernate-java8:5.4.3.Final'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
    implementation 'com.zaxxer:HikariCP:3.3.1'
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'net.logstash.logback:logstash-logback-encoder:6.1'
    implementation 'com.microsoft.azure:applicationinsights-spring-boot-starter:1.2.0-BETA'
    implementation 'com.microsoft.azure:applicationinsights-logging-logback:2.4.0-BETA'

    compileOnly 'org.projectlombok:lombok:1.18.8'

    runtime 'com.h2database:h2:1.4.199'

    testRuntime 'com.h2database:h2:1.4.199'

    testImplementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'junit:junit:4.12'
    testImplementation 'io.rest-assured:rest-assured:3.3.0'
    testImplementation 'com.github.tomakehurst:wiremock:2.23.2'
    testCompileOnly 'org.projectlombok:lombok:1.18.8'

}
